buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.ow2.asm:asm:9.6"
        classpath "org.ow2.asm:asm-commons:9.6"
    }
}

plugins {
    id 'java'
    id 'io.papermc.paperweight.userdev' version '1.7.1'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.vitor'
version = '1.2.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

dependencies {
    paperweight.paperDevBundle("1.21.3-R0.1-SNAPSHOT")
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation("org.bstats:bstats-bukkit:3.1.0")
}

tasks.named("shadowJar", com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    archiveClassifier.set('')
    configurations = [project.configurations.runtimeClasspath]
    relocate 'org.bstats', 'com.vitor.vPortrait.bstats'
}

assemble.dependsOn shadowJar

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}